plugins {
	id 'java'
	id 'io.freefair.aspectj.post-compile-weaving' version '8.7.1'
}

defaultTasks 'clean', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}


dependencies {

	var aws_powertools_version = "1.18.0"
	var dependencyVersions = [
		// ---------------------------------------------------------
		// cri_common_lib dependencies should match the ipv-cri-lib version
		// Workaround until dependency resolution is fix.
		// ---------------------------------------------------------
		cri_common_lib_version             : "1.5.4",

		// CRI_LIB aws
		aws_sdk_version                    : "2.30.15",
		aws_lambda_events_version          : "3.11.0",

		// CRI_LIB nimbus
		nimbusds_oauth_version             : "11.19.1",

		// CRI_LIB powertools
		aws_powertools_logging_version     : "${aws_powertools_version}",
		aws_powertools_metrics_version     : "${aws_powertools_version}",
		aws_powertools_parameters_version  : "${aws_powertools_version}",

		// ---------------------------------------------------------
		// AC Test Dependencies (Passport CRI)
		// ---------------------------------------------------------

		aws_lambda_core_version            : "1.2.1",
		// Object mapper
		jackson_version                    : "2.17.1",
		// Code weaving (lombok+powertools)
		aspectjrt_version                  : "1.9.21",

		// Test
		junit_version                      : "5.11.4",
		hamcrest_version                   : "2.2",
		mockito_version                    : "4.3.1",
		// testFixturesImplementation

		// acceptance tests Implementation
		// Update these together
		cucumber_version                   : "7.20.1",
		selenium_version                   : "4.26.0",
		axe_core_selenium_version          : "4.10.1",
		webdrivermanager_version           : "5.9.2",

		// acceptance tests testImplementation
		rest_assured_version               : "5.4.0",
		cucumber_junit_version             : "7.20.1"
	]

	implementation "software.amazon.awssdk:kms:${dependencyVersions.aws_sdk_version}",
			"com.fasterxml.jackson.core:jackson-core:${dependencyVersions.jackson_version}",
			"com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${dependencyVersions.jackson_version}",
			"com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${dependencyVersions.jackson_version}",
			"com.fasterxml.jackson.core:jackson-databind:${dependencyVersions.jackson_version}",
			"com.fasterxml.jackson.core:jackson-annotations:${dependencyVersions.jackson_version}",
			"com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbusds_oauth_version}" ,
			"org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit_version}",
			"org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit_version}",
			"org.junit.jupiter:junit-jupiter-params:${dependencyVersions.junit_version}",
			"org.mockito:mockito-junit-jupiter:${dependencyVersions.mockito_version}",
			"org.mockito:mockito-inline:${dependencyVersions.mockito_version}",
			"org.hamcrest:hamcrest:${dependencyVersions.hamcrest_version}",
			"com.google.code.gson:gson:${dependencyVersions.gson_version}",
			"org.json:json:20240303"


	implementation "io.cucumber:cucumber-java:${dependencyVersions.cucumber_version}",
			"org.seleniumhq.selenium:selenium-java:${dependencyVersions.selenium_version}",
			"io.github.bonigarcia:webdrivermanager:${dependencyVersions.webdrivermanager_version}",
			"com.deque.html.axe-core:selenium:${dependencyVersions.axe_core_selenium_version}"

	testImplementation "io.rest-assured:rest-assured:${dependencyVersions.rest_assured_version}",
			"io.cucumber:cucumber-junit:${dependencyVersions.cucumber_junit_version}"

	aspect "software.amazon.lambda:powertools-logging:${dependencyVersions.aws_powertools_logging_version}",
			"software.amazon.lambda:powertools-metrics:${dependencyVersions.aws_powertools_metrics_version}",
			"software.amazon.lambda:powertools-parameters:${dependencyVersions.aws_powertools_parameters_version}",
			"org.aspectj:aspectjrt:${dependencyVersions.aspectjrt_version}"
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

test {
	useJUnitPlatform()
	systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
}

//*************************************//
//                                     //
//    Test tasks                       //
//                                     //
//*************************************//

tasks.register('cucumber') {
	dependsOn assemble, testClasses

	doLast {
		javaexec {
			systemProperties = [
				'cucumber.tags': "${tags}"
			]
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--plugin',
				'html:target/cucumber-report/index.html',
				'--tags',
				"${tags}",
				'--glue',
				'uk/gov/di/ipv/cri/passport/acceptance_tests/step_definitions',
				'src/test/resources',
				'--plugin',
				'html:build/test-results/cucumber.html',
				'--plugin',
				'json:build/test-results/cucumber.json'
			]
		}
	}
}

tasks.register('smokeTestBuild') {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--plugin',
				'html:target/cucumber-report/index.html',
				'--glue',
				'uk/gov/di/ipv/cri/passport/acceptance_tests/step_definitions',
				'src/test/resources/features/',
				'--tags',
				'@smoke'
			]
		}
	}
}

tasks.register('smokeTestStaging') {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--plugin',
				'html:target/cucumber-report/index.html',
				'--glue',
				'uk/gov/di/ipv/cri/passport/acceptance_tests/step_definitions',
				'src/test/resources/features/',
				'--tags',
				'@staging'
			]
		}
	}
}

apply plugin: 'java'
